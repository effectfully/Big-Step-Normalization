open import Basic

infix 4 ⟦_⟧_⇓_ ⟦_⟧ˢᵘᵇ_⇓_ _$_⇓_ Quoteⁿᶠ_⇓_ Quoteⁿᵉ_⇓_ Norm_⇓_

mutual
  data ⟦_⟧_⇓_ : ∀ {Γ Δ σ} -> Γ ⊢ σ -> ⟦ Γ ↦ Δ ⟧ -> ⟦ Δ ⊢ⁿᶠ σ ⟧ -> Set where
    ø⇓      : ∀ {Γ Δ σ} {xˢ : ⟦ Δ ⊢ⁿᶠ σ ⟧} {ρ : ⟦ Γ ↦ Δ ⟧}
            -> ⟦ ø ⟧ (ρ ▻ᵉⁿᵛ xˢ) ⇓ xˢ
    ƛ⇓_     : ∀ {Γ Δ σ τ} {b : Γ ▻ σ ⊢ τ} {ρ : ⟦ Γ ↦ Δ ⟧}
            -> ⟦ ƛ b ⟧ ρ ⇓ ƛˢᵉᵐ b ρ
    _∙⟨_⟩⇓_ : ∀ {Γ Δ σ τ} {f : Γ ⊢ σ ⇒ τ} {x : Γ ⊢ σ} {ρ : ⟦ Γ ↦ Δ ⟧} {fˢʳ xˢʳ yˢʳ}
            -> ⟦ f ⟧ ρ ⇓ fˢʳ -> fˢʳ $ xˢʳ ⇓ yˢʳ -> ⟦ x ⟧ ρ ⇓ xˢʳ -> ⟦ f ∙ x ⟧ ρ ⇓ yˢʳ
    _[_]⇓   : ∀ {Γ Δ Θ σ} {x : Γ ⊢ σ} {ψ : Γ ↦ Δ} {ρ : ⟦ Δ ↦ Θ ⟧} {xʳ ρʳ}
            -> ⟦ x ⟧ ρʳ ⇓ xʳ -> ⟦ ψ ⟧ˢᵘᵇ ρ ⇓ ρʳ -> ⟦ x [ ψ ] ⟧ ρ ⇓ xʳ

  data ⟦_⟧ˢᵘᵇ_⇓_ : ∀ {Γ Δ Θ} -> Γ ↦ Δ -> ⟦ Δ ↦ Θ ⟧ -> ⟦ Γ ↦ Θ ⟧ -> Set where
    idˢᵘᵇ⇓  : ∀ {Γ Δ} {ρ : ⟦ Γ ↦ Δ ⟧}
            -> ⟦ idˢᵘᵇ ⟧ˢᵘᵇ ρ ⇓ ρ
    ↑⇓      : ∀ {Γ Δ σ} {xˢ : ⟦ Δ ⊢ⁿᶠ σ ⟧} {ρ : ⟦ Γ ↦ Δ ⟧}
            -> ⟦ ↑ ⟧ˢᵘᵇ ρ ▻ᵉⁿᵛ xˢ ⇓ ρ
    _▻ˢᵘᵇ⇓_ : ∀ {Γ Δ Θ σ} {x : Δ ⊢ σ} {ψ : Γ ↦ Δ} {ρ : ⟦ Δ ↦ Θ ⟧} {xˢʳ ρʳ}
            -> ⟦ ψ ⟧ˢᵘᵇ ρ ⇓ ρʳ -> ⟦ x ⟧ ρ ⇓ xˢʳ -> ⟦ ψ ▻ˢᵘᵇ x ⟧ˢᵘᵇ ρ ⇓ (ρʳ ▻ᵉⁿᵛ xˢʳ)
    _∘ˢᵘᵇ⇓_ : ∀ {Γ Δ Θ Ξ} {φ : Δ ↦ Θ} {ψ : Γ ↦ Δ} {ρ : ⟦ Θ ↦ Ξ ⟧} {ρʳ ρʳʳ}
            -> ⟦ φ ⟧ˢᵘᵇ ρ ⇓ ρʳ -> ⟦ ψ ⟧ˢᵘᵇ ρʳ ⇓ ρʳʳ -> ⟦ φ ∘ˢᵘᵇ ψ ⟧ˢᵘᵇ ρ ⇓ ρʳʳ

  data _$_⇓_ : ∀ {Γ σ τ} -> ⟦ Γ ⊢ⁿᶠ σ ⇒ τ ⟧ -> ⟦ Γ ⊢ⁿᶠ σ ⟧ -> ⟦ Γ ⊢ⁿᶠ τ ⟧ -> Set where
    ne$⇓ : ∀ {Γ σ τ} {fˢ : ⟦ Γ ⊢ⁿᵉ σ ⇒ τ ⟧} {xˢ : ⟦ Γ ⊢ⁿᶠ σ ⟧}
         -> neˢᵉᵐ fˢ $ xˢ ⇓ neˢᵉᵐ (fˢ ∙ xˢ)
    ƛ$⇓_ : ∀ {Γ Δ σ τ} {b : Γ ▻ σ ⊢ τ} {ρ : ⟦ Γ ↦ Δ ⟧} {xˢ : ⟦ Δ ⊢ⁿᶠ σ ⟧} {bˢʳ}
         -> ⟦ b ⟧ ρ ▻ᵉⁿᵛ xˢ ⇓ bˢʳ -> ƛˢᵉᵐ b ρ $ xˢ ⇓ bˢʳ

mutual
  data Quoteⁿᶠ_⇓_ : ∀ {Γ σ} -> ⟦ Γ ⊢ⁿᶠ σ ⟧ -> Γ ⊢ⁿᶠ σ -> Set where
    ne⇓ : ∀ {Γ} {xˢ : ⟦ Γ ⊢ⁿᵉ ⋆ ⟧} {xʳ}
        -> Quoteⁿᵉ xˢ ⇓ xʳ -> Quoteⁿᶠ (neˢᵉᵐ xˢ) ⇓ neⁿᶠ xʳ
    ƛ⇓_ : ∀ {Γ σ τ} {fˢ : ⟦ Γ ⊢ⁿᶠ σ ⇒ τ ⟧} {bˢʳ bʳʳ}
        -> pop⟦⟧ⁿᶠ fˢ $ varˢᵉᵐ vz ⇓ bˢʳ -> Quoteⁿᶠ bˢʳ ⇓ bʳʳ -> Quoteⁿᶠ fˢ ⇓ ƛⁿᶠ bʳʳ

  data Quoteⁿᵉ_⇓_ : ∀ {Γ σ} -> ⟦ Γ ⊢ⁿᵉ σ ⟧ -> Γ ⊢ⁿᵉ σ -> Set where
    var⇓ : ∀ {Γ σ} {v : σ ∈ Γ}
         -> Quoteⁿᵉ (var v) ⇓ var v
    _∙⇓_ : ∀ {Γ σ τ} {fˢ : ⟦ Γ ⊢ⁿᵉ σ ⇒ τ ⟧} {xˢ : ⟦ Γ ⊢ⁿᶠ σ ⟧} {fʳ xʳ}
         -> Quoteⁿᵉ fˢ ⇓ fʳ -> Quoteⁿᶠ xˢ ⇓ xʳ -> Quoteⁿᵉ (fˢ ∙ xˢ) ⇓ (fʳ ∙ xʳ)

data Norm_⇓_ : ∀ {Γ σ} -> Γ ⊢ σ -> Γ ⊢ⁿᶠ σ -> Set where
  norm⇓ : ∀ {Γ σ} {x : Γ ⊢ σ} {xˢʳ xʳʳ}
        -> ⟦ x ⟧ idᵉⁿᵛ ⇓ xˢʳ -> Quoteⁿᶠ xˢʳ ⇓ xʳʳ -> Norm x ⇓ xʳʳ
